/*
 * Copyright (c) Microsoft Corporation. All rights reserved.
 * Licensed under the MIT License.
 */

//
// :apps:sasquatch
//

evaluationDependsOn(':apps')

android {

    flavorDimensions "dependency", "distribute"

    defaultConfig {
        versionCode 304
        versionName "5.0.5"
        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK=Application.mk", "V=1"
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }

    productFlavors {
        projectDependency {
            dimension "dependency"
            applicationIdSuffix ".project"
            buildConfigField "String", "APP_SECRET", "\"${System.getenv("ANDROID_INT")}\""
            buildConfigField "String", "TARGET_TOKEN", "\"${System.getenv("ANDROID_TARGET_TOKEN_INT")}\""
            buildConfigField "String", "TARGET_TOKEN1", "\"${System.getenv("ANDROID_TARGET_TOKEN1_INT")}\""
            buildConfigField "String", "TARGET_TOKEN2", "\"${System.getenv("ANDROID_TARGET_TOKEN2_INT")}\""
            buildConfigField "String", "TARGET_TOKEN3", "\"${System.getenv("ANDROID_TARGET_TOKEN3_INT")}\""
        }
        mavenCentralDependency {
            dimension "dependency"
            applicationIdSuffix ".mavenCentral"
            buildConfigField "String", "APP_SECRET", "\"${System.getenv("ANDROID_PROD")}\""
            buildConfigField "String", "TARGET_TOKEN", "\"${System.getenv("ANDROID_TARGET_TOKEN_PROD")}\""
            buildConfigField "String", "TARGET_TOKEN1", "\"${System.getenv("ANDROID_TARGET_TOKEN1_PROD")}\""
            buildConfigField "String", "TARGET_TOKEN2", "\"${System.getenv("ANDROID_TARGET_TOKEN2_PROD")}\""
            buildConfigField "String", "TARGET_TOKEN3", "\"${System.getenv("ANDROID_TARGET_TOKEN3_PROD")}\""
        }
        appCenter {
            dimension "distribute"
        }
        googlePlay {
            dimension "distribute"
        }
    }

    buildTypes {
        releaseProguard {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['release']
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "Android.mk"
        }
    }
}

repositories {

    // Load credentials from local.properties file.
    File propertiesFile = project.rootProject.file('local.properties')
    def AZURE_USERNAME = null
    def AZURE_PASSWORD = null

    // Check that file exist.
    if (propertiesFile.exists()) {
        Properties properties = new Properties()
        properties.load(propertiesFile.newDataInputStream())

        // Get properties. If properties aren't exist it returns null.
        AZURE_USERNAME = properties.getProperty('AZURE_USERNAME')
        AZURE_PASSWORD = properties.getProperty('AZURE_PASSWORD')
    }

    maven {
        credentials {
            username = AZURE_USERNAME
            password = AZURE_PASSWORD
        }
        url "https://msmobilecenter.pkgs.visualstudio.com/_packaging/AppCenter/maven/v1/"
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.browser:browser:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    projectDependencyImplementation project(':sdk:appcenter-analytics')
    projectDependencyImplementation project(':sdk:appcenter-crashes')

    def appCenterSdkVersion = "5.0.4"
    mavenCentralDependencyImplementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    mavenCentralDependencyImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
    if (taskRequests.contains("GooglePlay")) {
        projectDependencyImplementation project(':sdk:appcenter-distribute-play')
        mavenCentralDependencyImplementation "com.microsoft.appcenter:appcenter-distribute-play:${appCenterSdkVersion}"
    } else {
        projectDependencyImplementation project(':sdk:appcenter-distribute')
        mavenCentralDependencyImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    }

    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.0'
}
